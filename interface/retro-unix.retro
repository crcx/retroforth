# RETRO on Unix - Listener and Extensions

In this file I am implementing the interactive listener that
RETRO will run when started with `-i`, `-i,c`, or `-i,fs`.

~~~
:image:save (:s-) #1000 io:scan-for io:invoke ;
~~~

Now that I can read characters, it's time to support reading
strings. I do this via two words. The first is `parse-until`.
This will setup a temporary string as an input buffer, then
read input, passing each character ot a provided quote. When
the quote returns `TRUE`, it ends and returns the string. When
not `TRUE` it will add the character to the buffer.

~~~
{{
  (c-cf) :edit?  dup [ ASCII:BS eq? ] [ ASCII:DEL eq? ] bi or ;
  (-f)   :ended? buffer:size @TempStringMax gteq? ;
  (c-)   :add    ended? &drop &buffer:add choose ;
  (c-)   :gather edit? &drop &add choose ;
  (q-qc) :cycle  repeat c:get dup-pair swap call not 0; drop gather again ;
---reveal---
  :parse-until (:q-s)
    [ s:empty buffer:set cycle drop-pair buffer:start ] buffer:preserve ;
}}
~~~

Using this, a simple `s:get` can be implemented very easily as
a quote which looks for an end of line character.

~~~
:s:get (:-s) [ [ ASCII:CR eq? ]
              [ ASCII:LF eq? ] bi or ] parse-until ;
~~~

~~~
:clear (:-) '\^[2J\^[0;0H s:format s:put ;
~~~

Hide the support words.

# Standard Interactive Listener

The main part of this file is the *listener*, an interactive
read-eval-print loop.

RRE's C part will access a couple parts of this, based on the
startup flags passed.

~~~
:// (:-)   script:ignore-to-eol &Ignoring v:on ; immediate
~~~

## d:source

~~~
'interface/retro-unix.retro s:keep
dup '// d:lookup d:source store
dup 'clear d:lookup d:source store
dup 's:get d:lookup d:source store
dup 'parse-until d:lookup d:source store
dup 'image:save d:lookup d:source store
drop
~~~

~~~
'Save_the_current_system_to_a_new_image_file. 'image:save d:set-description
'Read_input_from_stdin_(via_`c:get`)_until_the_returned_character_is_matched_by_the_quote._Returns_a_string. 'parse-until d:set-description
'Read_input_from_standard_in_(via_`c:get`)_until_a_CR_or_LF_is_encountered._Returns_a_string. 's:get d:set-description
'Clear_the_display. 'clear d:set-description
'Ignore_any_tokens_remaining_on_the_current_line._Works_with_files_and_the_interactive_`listener`. '// d:set-description
~~~
